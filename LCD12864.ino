#include <dht11.h>  //get tempeature
#include <U8glib.h>  //drive lcd12864
#include <DS1302.h>  //get accurate time
#include <EEPROM.h>  //write data to EEPROM

byte hour, minute;
const int BuzzerPin = 2;
const int TemPin =  3;
const int RS = 4;
const int WR = 5;
const int EN = 6;
const int RST = 7;
const int DAT = 8;
const int CLOCK = 9;
const int CLK = 10;
const int ConfPin = 11;
const int DT = 12;
boolean currentStateCLK;
boolean lastStateCLK;
boolean isPressed = true;
boolean isAlarm;
unsigned long int LastTime;
const int menuItems = 4;
const int ClockWidth = 96;
const int ClockHeight = 32;
const char *myMenus[] = {"Back", "Set Alarm", "Alarm On/Off", "Booting up Cartoons"};
const char *myDays[] =  {"Sun", "Mon", "Tue", "Wed", "Thu", "Fri", "Sat"};
const unsigned char Ring[] U8G_PROGMEM = {
  0x00, 0x18, 0x3C, 0x3C, 0x3C, 0x7E, 0x18, 0x00
};  //the ring icon
const unsigned char UpArrow[] U8G_PROGMEM = {
  0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x80, 0x01, 0xC0, 0x03, 0xE0, 0x07, 0x80, 0x01, 0x80, 0x01,
  0x80, 0x01, 0x80, 0x01, 0x80, 0x01, 0x80, 0x01, 0x80, 0x01, 0x80, 0x01, 0x00, 0x00, 0x00, 0x00
};  //the uparrow icon
const unsigned char AlarmClock[] U8G_PROGMEM = {
  0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x70, 0x00, 0x00, 0x00,
  0x88, 0x00, 0x00, 0x0E, 0x48, 0xE0, 0x03, 0x11, 0x28, 0x98, 0x1C, 0x12, 0x10, 0x86, 0x60, 0x14,
  0x00, 0x01, 0x80, 0x08, 0x80, 0x00, 0x00, 0x01, 0x40, 0x00, 0x00, 0x02, 0x20, 0x00, 0x20, 0x04,
  0x20, 0x00, 0x10, 0x04, 0x10, 0x00, 0x08, 0x08, 0x10, 0x00, 0x04, 0x08, 0x10, 0x00, 0x02, 0x08,
  0x70, 0xFC, 0x01, 0x0E, 0x10, 0x00, 0x00, 0x08, 0x10, 0x00, 0x00, 0x08, 0x10, 0x00, 0x00, 0x08,
  0x20, 0x00, 0x00, 0x04, 0x20, 0x00, 0x00, 0x04, 0x40, 0x00, 0x00, 0x02, 0x80, 0x00, 0x00, 0x01,
  0x00, 0x01, 0x80, 0x00, 0x00, 0x86, 0x60, 0x00, 0x00, 0x9B, 0xDC, 0x00, 0x80, 0xE1, 0x83, 0x01,
  0xC0, 0x00, 0x00, 0x03, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
};  //the bell icon
const unsigned char arduinoLogo[] U8G_PROGMEM = {
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0x03, 0xFC, 0xFF, 0x01, 0x7E, 0xFE, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0x00, 0xF0, 0x7F, 0x00, 0x78, 0xFE, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0x7F, 0x00, 0xE0, 0x3F, 0x00, 0xF0, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0x3F, 0x00, 0xC0, 0x1F, 0x00, 0xE0, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0x1F, 0xF0, 0x80, 0x0F, 0x78, 0xE0, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0x1F, 0xFC, 0x03, 0x07, 0xFE, 0xC1, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0x0F, 0xFE, 0x07, 0x03, 0xFF, 0x83, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0x0F, 0xFF, 0x0F, 0x82, 0xFF, 0x87, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0x87, 0xFF, 0x1F, 0xC0, 0xFF, 0x87, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xC7, 0xFF, 0x1F, 0xE0, 0xCF, 0x0F, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xE3, 0xFF, 0x3F, 0xE0, 0xCF, 0x1F, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xE3, 0xFF, 0x7F, 0xF0, 0xCF, 0x1F, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xE3, 0x07, 0x7E, 0xF0, 0x01, 0x1E, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xE3, 0x07, 0x7E, 0xF0, 0x01, 0x1E, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xE3, 0xFF, 0x7F, 0xF0, 0xCF, 0x1F, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xE3, 0xFF, 0x3F, 0xE0, 0xCF, 0x1F, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xC3, 0xFF, 0x3F, 0xE0, 0xCF, 0x0F, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0x87, 0xFF, 0x1F, 0xC0, 0xFF, 0x87, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0x0F, 0xFF, 0x0F, 0x82, 0xFF, 0x87, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0x0F, 0xFE, 0x07, 0x03, 0xFF, 0x83, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0x1F, 0xFC, 0x03, 0x07, 0xFE, 0xC1, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0x1F, 0xF0, 0x80, 0x0F, 0x78, 0xC0, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0x3F, 0x00, 0xC0, 0x1F, 0x00, 0xE0, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0x7F, 0x00, 0xE0, 0x3F, 0x00, 0xF0, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0x00, 0xF0, 0x7F, 0x00, 0xF8, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0x01, 0xFC, 0xFF, 0x01, 0xFE, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0x3F, 0xC7, 0x71, 0x17, 0xE4, 0xC6, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0x1F, 0xD7, 0x65, 0x77, 0xC7, 0x92, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0x5F, 0xB7, 0x6D, 0x77, 0xD7, 0xBA, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0x5F, 0xB7, 0x6D, 0x77, 0x97, 0xBA, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0x5F, 0x86, 0x6D, 0x77, 0xB7, 0xBA, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0x0F, 0xB6, 0x6D, 0x77, 0xB7, 0xBA, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xEF, 0xB6, 0x65, 0x77, 0x77, 0x92, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xEF, 0xB6, 0xF1, 0x18, 0x74, 0xC6, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0x03, 0x7F, 0xE0, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0x7D, 0x7F, 0xDF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0x3D, 0x7F, 0xDE, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xCE, 0x80, 0xB9, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0x32, 0x7F, 0xB2, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xDD, 0xF7, 0xCD, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xEF, 0xFF, 0xFB, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xF7, 0x7F, 0xF7, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xF3, 0xBF, 0xF7, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFB, 0xDF, 0xEF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFB, 0xCF, 0xEF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0x1B, 0xEF, 0xEF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFB, 0xF0, 0xEF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFB, 0xFF, 0xEF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFB, 0xFF, 0xF7, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xF7, 0xFF, 0xF7, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xE7, 0xFF, 0xFB, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xCF, 0xF7, 0xFD, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0x9F, 0xF7, 0xFE, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0x3F, 0x1C, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xDF, 0xF7, 0xFE, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
  0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF, 0xFF,
};

dht11 DHT11;
U8GLIB_ST7920_128X64 u8g(EN, WR, RS, U8G_PIN_NONE);
DS1302 rtc(RST, DAT, CLOCK);

void bootingUp() {
  u8g.firstPage();
  do {
    u8g.drawXBMP( 0, 0, 128, 64, arduinoLogo);
  } while ( u8g.nextPage() );
  delay(3000);
}

//draw the emulated clock hands
void DrawHand(float time, float steps, int radius)
{
  double angle = time * 2.0 * 3.1415 / steps - 1;
  double X = ClockWidth + radius * cos(angle);
  double Y = ClockHeight + radius * sin(angle);
  u8g.drawLine(ClockWidth, ClockHeight, X, Y);
}

//print main UI
void PrintWin() {
  u8g.firstPage();
  do {
    //print date
    Time time = rtc.time();
    DHT11.read(TemPin);
    u8g.setColorIndex(1);
    u8g.drawRFrame(0, 0, 128, 64, 4);
    u8g.drawRFrame(68, 4, 57, 57, 3);
    if (isAlarm)u8g.drawXBMP( 58, 26, 8, 8, Ring);
    if (isAlarm == true && time.hr == hour && time.min == minute) {
      u8g.drawXBMP( 18, 25, 32, 32, AlarmClock);
      tone(BuzzerPin, 300);
      if (!digitalRead(ConfPin)) {
        isAlarm = !isAlarm;
        noTone(BuzzerPin);
        LastTime = millis();
      }
    }
    else {
      u8g.setFont(u8g_font_5x8);
      u8g.setPrintPos(7, 55);
      u8g.print(time.yr);
      u8g.drawStr( 29, 55, "/");
      u8g.setPrintPos(36, 55);
      u8g.print(time.mon);
      u8g.drawStr( 43, 55, "/");
      u8g.setPrintPos(51, 55);
      u8g.print(time.date);
      u8g.setFont(u8g_font_6x13);
      u8g.setPrintPos(10, 41);
      u8g.print(myDays[time.day - 1]);
      u8g.setPrintPos(33, 41);
      u8g.print(DHT11.temperature);
      u8g.print(char(176));
      u8g.print('C');
    }

    //print time
    u8g.drawRBox(3, 4, 62, 21, 2);
    u8g.setColorIndex(0);
    u8g.setFont(u8g_font_fub17);
    u8g.drawStr(29, 21, ":");
    int position;
    if (time.hr < 10)
    {
      u8g.drawStr(3, 23, "0");
      position = 16;
    }
    else position = 3;
    u8g.setPrintPos(position, 23);
    u8g.print(time.hr);
    if (time.min < 10)
    {
      u8g.drawStr(38, 23, "0");
      position = 51;
    }
    else position = 38;
    u8g.setPrintPos(position , 23);
    u8g.print(time.min);
    u8g.setColorIndex(1);

    //print emulated clock
    u8g.drawCircle(ClockWidth, ClockHeight, 26);
    u8g.drawCircle(ClockWidth, ClockHeight, 1);
    u8g.setFont(u8g_font_6x13);
    u8g.drawStr(90, 17, "12");
    u8g.drawStr(114, 37, "3");
    u8g.drawStr(94, 56, "6");
    u8g.drawStr(72, 37, "9");
    DrawHand(time.hr - 1, 12.0, 10);
    DrawHand(time.min - 5, 60.0, 19);
    DrawHand(time.sec - 5, 60, 21);
  } while ( u8g.nextPage() );
}

void AlarmOnOff() {
  isAlarm = !isAlarm;
  EEPROM.write(0, isAlarm);
  u8g.firstPage();
  do {
    u8g.setFontPosTop();
    u8g.setFont(u8g_font_unifont);
    u8g.drawRFrame(0, 0, 128, 64, 4);
    u8g.drawRBox(27, 22, u8g.getStrWidth("Finished!") + 3, 17, 2);
    u8g.setColorIndex(0);
    u8g.drawStr(30, 24, "Finished!");
    u8g.setColorIndex(1);
  } while ( u8g.nextPage() );
  delay(1000);
}

//print setting clock UI
void printSetClock(int timeArrey[], int position, boolean isChoice) {
  u8g.firstPage();
  do {
    u8g.setFontPosTop();
    u8g.setFont(u8g_font_fub17);
    u8g.drawRFrame(0, 0, 128, 64, 4);
    u8g.setPrintPos(25 + 16 * 2 , 26);
    u8g.print(':');
    for (int i = 0; i < 4; i++) {
      if (i < 2)
        u8g.setPrintPos(25 + 16 * i, 28);
      else
        u8g.setPrintPos(20 + 16 * (i + 1), 28);
      u8g.print(timeArrey[i]);
    }
    if (position < 4) {
      u8g.setFont(u8g_font_5x8);
      u8g.drawStr(45, 55, "Confirm");
      u8g.drawStr(90, 55, "Cancle");
    }
    else if (position == 4 && isChoice) {
      u8g.setFont(u8g_font_5x8);
      u8g.drawRBox(43, 47, u8g.getStrWidth("Confirm") + 4, 10, 2);
      u8g.setColorIndex(0);
      u8g.drawStr(45, 55, "Confirm");
      u8g.setColorIndex(1);
      u8g.drawStr(90, 55, "Cancle");
    }
    else if (position == 4 && !isChoice) {
      u8g.setFont(u8g_font_5x8);
      u8g.drawRBox(88, 47, u8g.getStrWidth("Cancle") + 4, 10, 2);
      u8g.setColorIndex(0);
      u8g.drawStr(90, 55, "Cancle");
      u8g.setColorIndex(1);
      u8g.drawStr(45, 55, "Confirm");
    }
    if (position < 2)
      u8g.drawXBMP( 25 + 16 * position, 30, 16, 16, UpArrow);
    else if (position < 4)
      u8g.drawXBMP( 20 + 16 * (position + 1), 30, 16, 16, UpArrow);
  } while ( u8g.nextPage() );
}

//set clock
void SetClock() {
  LastTime = millis();
  int hour1 = hour / 10, hour2 = hour % 10, minute1 = minute / 10, minute2 = minute % 10;
  int timeArrey[] = {hour1, hour2, minute1, minute2};
  int position = 0;
  boolean isChoice = true;
  while (1) {
    isPressed = digitalRead(ConfPin);
    if (!isPressed && millis() - LastTime > 500) {
      position ++;
      LastTime = millis();
      if (position == 5) {
        int hou = timeArrey[0] * 10 + timeArrey[1];
        int minu = timeArrey[2] * 10 + timeArrey[3];
        if (hou < 24 && minu < 60 && isChoice) {
          isAlarm = true;
          hour = hou;
          minute = minu;
          EEPROM.write(0, isAlarm);//write data to EEPROM
          EEPROM.write(1, hour);
          EEPROM.write(2, minute);
          u8g.firstPage();
          do {
            u8g.setFontPosTop();
            u8g.setFont(u8g_font_unifont);
            u8g.drawRFrame(0, 0, 128, 64, 4);
            u8g.drawRBox(27, 22, u8g.getStrWidth("Finished!") + 3, 17, 2);
            u8g.setColorIndex(0);
            u8g.drawStr(30, 24, "Finished!");
            u8g.setColorIndex(1);
          } while ( u8g.nextPage() );
        }
        else {
          u8g.firstPage();
          do {
            u8g.setFontPosTop();
            u8g.setFont(u8g_font_unifont);
            u8g.drawRFrame(0, 0, 128, 64, 4);
            u8g.drawRBox(27, 22, u8g.getStrWidth("Cancled!") + 3, 17, 2);
            u8g.setColorIndex(0);
            u8g.drawStr(30, 24, "Cancled!!");
            u8g.setColorIndex(1);
          } while ( u8g.nextPage() );
        }
        delay(1000);
        break;
      }
    }
    currentStateCLK = digitalRead(CLK);
    if (currentStateCLK != lastStateCLK) {
      if (digitalRead(DT) != currentStateCLK)
        if (position < 4) {
          if (timeArrey[position] > 0)
            timeArrey[position]--;
          else if (position == 0 && timeArrey[position] < 2)
            timeArrey[position]++;
          else if (position == 1 && timeArrey[position] < 9)
            timeArrey[position]++;
          else if (position == 2 && timeArrey[position] < 6)
            timeArrey[position]++;
          else if (position == 3 && timeArrey[position] < 9)
            timeArrey[position]++;
        }
        else {
          isChoice = !isChoice;
        }
    }
    lastStateCLK = currentStateCLK;
    printSetClock(timeArrey, position, isChoice);
  }
}

//print menu UI
void PrintMenu(int count) {
  u8g.firstPage();
  u8g.setFontPosTop();
  do {
    u8g.setFont(u8g_font_6x13);
    u8g.drawRFrame(0, 0, 128, 64, 4);
    u8g.drawRBox(8, 3 + 14 * count, u8g.getStrWidth(myMenus[count]) + 3, 15, 2);
    u8g.setColorIndex(0);
    u8g.drawStr(10, 4 + 14 * count, myMenus[count]);
    u8g.setColorIndex(1);
    for (int i = 0; i < menuItems; i++) {
      if (i != count)
        u8g.drawStr(10, 4 + 14 * i, myMenus[i]);
    }
  }
  while ( u8g.nextPage() );
}

//select menu
void SelectMenu() {
  int count = 0;
  while (1) {
    isPressed = digitalRead(ConfPin);
    if (!isPressed && millis() - LastTime > 500) {
      LastTime = millis();
      switch (count) {
        case 0: ; break;
        case 1: SetClock(); break;
        case 2: AlarmOnOff(); break;
        case 3: bootingUp(); break;
      }
      break;
    }
    else {
      currentStateCLK = digitalRead(CLK);
      if (currentStateCLK != lastStateCLK  && currentStateCLK == LOW) {
        if (digitalRead(DT) != currentStateCLK) {
          if (count > 0)
            count--;
        }
        else if (count < 3)
          count++;
      }
      PrintMenu(count);
      lastStateCLK = currentStateCLK;
    }
  }
}

void setup() {
  pinMode(CLK, INPUT_PULLUP);
  pinMode(ConfPin, INPUT_PULLUP);
  pinMode(DT, INPUT_PULLUP);
  isAlarm = (boolean)EEPROM.read(0);
  hour = EEPROM.read(1);
  minute = EEPROM.read(2);
  u8g.setFontPosTop();
  LastTime = millis();
  noTone(BuzzerPin);
  bootingUp();
}

void loop() {
  isPressed = digitalRead(ConfPin);
  if (!isPressed && millis() - LastTime > 500) {
    LastTime = millis();
    SelectMenu();
  }
  else
    PrintWin();
}
